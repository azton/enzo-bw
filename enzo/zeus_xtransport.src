#include "fortran.def"
#include "error.def"
c=======================================================================
c/////////////////////  SUBROUTINE ZEUS_XTRANSPORT  \\\\\\\\\\\\\\\\\\\\
c
      subroutine zeus_xtransport(d, e, u, v, w, in, jn, kn, rank,
     &                           is, ie, js, je, ks, ke,
     &                           dt, dx, f1, bottom,
     &                           nsubgrids, lface, rface,
     &                           fistart, fiend, fjstart, fjend,
     &                           dindex, eindex, geindex,
     &                           uindex, vindex, windex, array,
     &                           ncolor, colorpt, coloff, colindex)
c
c  TRANSPORT TERM IN X-DIRECTION FOR ZEUS HYDRO (CARTESIAN ONLY)
c
c  written by: Greg Bryan (implemented from Stone & Norman, ApJS 80, 753)
c  date:       February, 1997
c  modified1:  Robert Harkness & Alexei Kritsuk
c              to ensure consistent minimum change factors in x, y and z
c  date:       June 2003
c
c  PURPOSE:
c
c  EXTERNALS:
c
c  INPUTS:
c     d       - density field (includes boundary zones)
c     dx,y,z  - zone width arrays for each dimension
c     e       - gas specific energy field
c     i,j,kn  - dimensions of field arrays
c     rank    - dimension of problem (not currently used)
c     u       - x-velocity field
c     v       - y-velocity field
c     w       - z-velocity field
c
c  LOCALS:
c
c-----------------------------------------------------------------------
      implicit NONE
c-----------------------------------------------------------------------
c
c     Arguments
c
      integer in, jn, kn, is, ie, js, je, ks, ke, rank, bottom,
     &        nsubgrids, ncolor, coloff(ncolor)
      integer fistart(nsubgrids*3), fiend(nsubgrids*3),
     &        fjstart(nsubgrids*3), fjend(nsubgrids*3), 
     &        lface(nsubgrids*3), rface(nsubgrids*3)
      integer dindex(nsubgrids*6), eindex(nsubgrids*6),
     &        uindex(nsubgrids*6), vindex(nsubgrids*6),
     &        windex(nsubgrids*6),geindex(nsubgrids*6),
     &        colindex(nsubgrids*6,ncolor)
      real    array(1), colorpt(1)
      real    d(in,jn,kn), e(in,jn,kn), u(in,jn,kn), v(in,jn,kn),
     &        w(in,jn,kn), f1(in,jn,kn), dx(in)
      real    dt
c
c     Parameters
c
      integer ijk
      parameter (ijk = MAX_ANY_SINGLE_DIRECTION)
c
c     Locals
c
      integer i, j, k, jm1, km1, jsm1, ksm1, jep1, kep1, i1,
     &        n, idim, offset, ic
      real   dnew, q(ijk), div(ijk), f2(ijk), f3(ijk), f4(ijk), f5(ijk),
     &       dstar(ijk), estar(ijk), ustar(ijk), vstar(ijk), wstar(ijk),
     &       uavgi(ijk), uavgj(ijk), uavgk(ijk), df, 
     &       colstar(ijk, MAX_COLOR), ueff(ijk)
      integer im1, ip1, im2, im3, ip2
c
#define NO_VELOCITY_AVERAGE
#define DENS_LIMIT 1000000.0
#define MIN_DENSITY_CHANGE 0.0
#define MIN_ENERGYF_CHANGE 0.0
c
c\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\////////////////////////////////
c=======================================================================
c
c     Compute variants on start indexes
c
      jsm1 = max(js-1, 1)
      ksm1 = max(ks-1, 1)
      jep1 = min(je+1, jn)
      kep1 = min(ke+1, kn)
c
c     1) Transport step - x direction
c
c     Compute the mass flux over the entire field
c
      do k = 1, kn
         do j = 1, jn
c
c           Interpolate density
c
            do i = 1, in
               ueff(i) = u(i,j,k)
            enddo
c
#ifdef VELOCITY_AVERAGE
            if (bottom .eq. 1) then
               do i = is-1, ie+2
c               if (d(im1,j,k)/d(i,j,k) .gt. 10.0 .or.
c     &             d(i,j,k)/d(im1,j,k) .gt. 10.0) 
                  im1 = max(i-1, is-2)
                  if (max(d(im1,j,k),d(i,j,k)) .gt. DENS_LIMIT) then
c
                     ip1 = min(i+1, ie+3)
                     im2 = max(i-2, is-2)
                     ip2 = min(i+2, ie+3)
                     im3 = max(i-3, is-2)
c
c                     ueff(i) = ((d(im3,j,k)+d(im2,j,k))*u(im2,j,k)+
c     &                          (d(im2,j,k)+d(im1,j,k))*u(im1,j,k)+
c     &                          (d(im1,j,k)+d(i  ,j,k))*u(i  ,j,k)+
c     &                          (d(i  ,j,k)+d(ip1,j,k))*u(ip1,j,k)+
c     &                          (d(ip1,j,k)+d(ip2,j,k))*u(ip2,j,k))/
c     &                      (    d(im3,j,k)+2.0*d(im2,j,k)+
c     &                       2.0*d(im1,j,k)+2.0*d(i  ,j,k)+
c     &                       2.0*d(ip1,j,k)+    d(ip2,j,k))
c
                     ueff(i) = ((d(im2,j,k)+d(im1,j,k))*u(im1,j,k)+
     &                        (d(im1,j,k)+d(i  ,j,k))*u(i  ,j,k)+
     &                        (d(i  ,j,k)+d(ip1,j,k))*u(ip1,j,k))/
     &                        (d(im2,j,k)+2.0*d(im1,j,k)+
     &                         d(ip1,j,k)+2.0*d(i  ,j,k))
                  endif
               enddo
            endif
#endif
c
            call vanlr1_zc(d, in, jn, kn, is-1, ie+2, j, k, 
     &                     dx, dt, ueff, dstar)
c     &                     dx, dt, u(1,j,k), dstar)
c
c           Compute mass flux
c
            do i = is-2, ie+3
               im1 = max(i-1, is-2)
c               f1(i,j,k) = dstar(i)*u(i,j,k)
               f1(i,j,k) = dstar(i)*ueff(i)
               if (abs(u(i,j,k)) .gt. 0.5*dx(i)/dt) write(6,*) 'xt1',
     &            u(i-1,j,k),u(i,j,k),u(i+1,j,k),dstar(i-1),
     &            dstar(i),dstar(i+1),i,j,k,d(i-1,j,k),d(i,j,k),
     &            d(i+1,j,k),dx(i),dx(i+1),dt
            enddo
c
         enddo
      enddo
c
c     Update the quantities (loop backwards on j,k so j-1,k-1 are old values)
c
      do k = kn, 1, -1
         do j = jn, 1, -1
            jm1 = max(j-1, 1)
            km1 = max(k-1, 1)
c
            do i = 1, in
               ueff(i) = u(i,j,k)
            enddo
c
#ifdef VELOCITY_AVERAGE
            if (bottom .eq. 1) then
               do i = is-1, ie+2
c
c               if (d(im1,j,k)/d(i,j,k) .gt. 2.0 .or.
c     &             d(i,j,k)/d(im1,j,k) .gt. 2.0) 
c
                  im1 = max(i-1, is-2)
                  if (max(d(im1,j,k),d(i,j,k)) .gt. DENS_LIMIT) then
                     ip1 = min(i+1, ie+3)
                     im2 = max(i-2, is-2)
                     ip2 = min(i+2, ie+3)
                     im3 = max(i-3, is-2)
c
c                     ueff(i) = ((d(im3,j,k)+d(im2,j,k))*u(im2,j,k)+
c     &                      (d(im2,j,k)+d(im1,j,k))*u(im1,j,k)+
c     &                      (d(im1,j,k)+d(i  ,j,k))*u(i  ,j,k)+
c     &                      (d(i  ,j,k)+d(ip1,j,k))*u(ip1,j,k)+
c     &                      (d(ip1,j,k)+d(ip2,j,k))*u(ip2,j,k))/
c     &                      (    d(im3,j,k)+2.0*d(im2,j,k)+
c     &                       2.0*d(im1,j,k)+2.0*d(i  ,j,k)+
c     &                       2.0*d(ip1,j,k)+    d(ip2,j,k))
c
                     ueff(i) = ((d(im2,j,k)+d(im1,j,k))*u(im1,j,k)+
     &                          (d(im1,j,k)+d(i  ,j,k))*u(i  ,j,k)+
     &                          (d(i  ,j,k)+d(ip1,j,k))*u(ip1,j,k))/
     &                          (d(im2,j,k)+2.0*d(im1,j,k)+
     &                           d(ip1,j,k)+2.0*d(i  ,j,k))
c
                  endif
               enddo
            endif
#endif
c
c           Interpolate energy
c
            call vanlr1_zc(e, in, jn, kn, is-1, ie+2, j, k,
     &                     dx, dt, ueff, estar)
c     &                     dx, dt, u(1,j,k), estar)
c
c           Compute color flux (assuming it is density-like) and advect
c              color variables (zone-centered)
c
            do ic = 1, ncolor
               call vanlr1_zc(colorpt(coloff(ic)+1),
     &                        in, jn, kn, is-1, ie+2, j, k,
     &                        dx, dt, ueff, colstar(1,ic))
c     &                        dx, dt, u(1,j,k), colstar(1,ic))
               do i = is-2, ie+3
                  colstar(i,ic) = colstar(i,ic) * ueff(i)
c                  colstar(i,ic) = colstar(i,ic) * u(i,j,k)
               enddo
               do i = is-2, ie+2
                  colorpt(coloff(ic)+((k-1)*jn+j-1)*in+i) =
     &              max(0.1*colorpt(coloff(ic)+((k-1)*jn+j-1)*in+i),
     &                      colorpt(coloff(ic)+((k-1)*jn+j-1)*in+i) +
     &                      dt*(colstar(i,ic) - colstar(i+1,ic))/dx(i))
               enddo
            enddo
c
c           Compute energy flux
c
            do i = is-2, ie+3
               f5(i) = estar(i)*f1(i,j,k)
            enddo
c
c           Make appropriately-averaged quanitities for advection
c
            do i=is-3, ie+2
c               uavgi(i) = 0.5*(u(i,j,k) + u(i+1,j,k)) ! yes, this is right
               uavgi(i) = 0.5*(ueff(i) + ueff(i+1)) ! yes, this is right
            enddo
            do i=is-3, ie+3
               uavgj(i) = 0.5*(u(i,j,k) + u(i,jm1,k))
               uavgk(i) = 0.5*(u(i,j,k) + u(i,j,km1))
            enddo
c
c           Interpolate velocities
c
            call vanlr1_fc(u, in, jn, kn, is-1, ie+1, j, k,
     &                     dx, dt, uavgi, ustar)
c            call vanlr1_fc(ueff, in, 1, 1, is-1, ie+1, 1, 1,
c     &                     dx, dt, uavgi, ustar)
            if (rank .gt. 1) call vanlr1_zc(v, in, jn, kn, is-1,ie+2,j,
     &                                      k, dx, dt, uavgj, vstar)
            if (rank .gt. 2) call vanlr1_zc(w, in, jn, kn, is-1,ie+2,j,
     &                                      k, dx, dt, uavgk, wstar)
c
c           Compute momentum fluxes
c
            do i = is-2, ie+2
               f2(i) = ustar(i)*0.5*(f1(i,j,k) + f1(i+1,j,k))
c               f2(i) = ustar(i)**2*d(i,j,k)
            enddo
!           if (rank .gt. 1) then
               do i = is-1, ie+2
                  f3(i) = vstar(i)*0.5*(f1(i,j,k) + f1(i,jm1,k))
                  f4(i) = wstar(i)*0.5*(f1(i,j,k) + f1(i,j,km1))
               enddo
!           endif
c
c           Convert velocities to momenta
c
            do i = is-1, ie+2
c               if (u(i,j,k) .gt. 5.0) write(6,*) 
c     &           u(i-1,j,k),u(i,j,k),u(i+1,j,k),
c     &           ueff(i-1),ueff(i),ueff(i+1),
c     &           d(i-1,j,k),d(i,j,k),d(i+1,j,k),f2(i),f2(i-1)
               u(i,j,k) = u(i,j,k)*0.5*(d(i,j,k) + d(i-1,j,k))
               u(i,j,k) = u(i,j,k) + dt*(f2(i-1)-f2(i))/dx(i)
            enddo
            do i = is-1, ie+1
               v(i,j,k) = v(i,j,k)*0.5*(d(i,j,k) + d(i,jm1,k))
               w(i,j,k) = w(i,j,k)*0.5*(d(i,j,k) + d(i,j,km1))
c
c              Update momentum fluxes
c
               v(i,j,k) = v(i,j,k) + dt*(f3(i)-f3(i+1))/dx(i)
               w(i,j,k) = w(i,j,k) + dt*(f4(i)-f4(i+1))/dx(i)
            enddo
c
c              Update mass and energy fluxes
c
            do i = is-2, ie+2

               dnew = max(
     &                    MIN_DENSITY_CHANGE*d(i,j,k),
     &                    d(i,j,k) + dt*(f1(i,j,k) - f1(i+1,j,k))/dx(i)
     &                   )

               if (dnew/d(i,j,k) .lt. 0.15) write(6,*) 'd',d(i,j,k),
     &              dnew,i,j,k,f1(i,j,k),f1(i+1,j,k)

c               if (j .eq. jn/2 .and. k .eq. kn/2) write(31,*)
c     &             i,f1(i,j,k),d(i,j,k),dnew,dx(i)
c               if (e(i,j,k) .gt. 50.0) write(6,*) i,j,k,
c     &            e(i,j,k), (e(i,j,k)*d(i,j,k) + 
c     &                dt*(f5(i) - f5(i+1))/dx(i))/dnew, f5(i),f5(i+1),
c     &                u(i-1,j,k),u(i,j,k)

               e(i,j,k) = max(
     &                        MIN_ENERGYF_CHANGE*e(i,j,k),
     &                        (e(i,j,k)*d(i,j,k) + 
     &                           dt*(f5(i) - f5(i+1))/dx(i))/dnew
     &                       )

               if (e(i,j,k) .le. 0.0 .or. dnew .le. 0.0) then
                  write(6,*) 'zxt',d(i,j,k),e(i,j,k),dnew,dt
                  ERROR_MESSAGE
               endif

               d(i,j,k) = dnew

            enddo
c
c              Convert momenta back to velocities
c
            do i = is-1, ie+2
               u(i,j,k) = u(i,j,k)/(0.5*(d(i,j,k) + d(i-1,j,k)))
c               if (e(i,j,k) .gt. 100.0) write(6,*) e(i,j,k),u(i-1,j,k),
c     &                u(i,j,k),d(i-1,j,k),d(i,j,k),d(i+1,j,k)
               if (abs(u(i,j,k)) .gt. dx(i)/dt) then
                  write(6,*) 'ux',i,j,k,ie,je,ke
                  write(6,*) dx(i),dt
                  write(6,*) u(i,j,k),d(i,j,k),d(i-1,j,k)
                  do i1=1, in
                     write(6,*) i1,d(i1,j,k),u(i1,j,k),v(i1,j,k)
                     write(6,*) i1,e(i1,j,k),w(i1,j,k),d(i1-1,j,k)
                     write(6,*) i1,f2(i1),ustar(i1),f1(i1,j,k),uavgi(i1)
                  enddo
                  ERROR_MESSAGE
               endif
            enddo
c
c  Check this slice against the list of subgrids 
c     (all subgrid quantities are zero based)
c
         do n=0, nsubgrids-1
c        do k = fjstart(n*3+1)+1, fjend(n*3+1)+1
           if (k .ge. fjstart(n*3+1)+1 .and. k .le. fjend(n*3+1)+1 .and.
     &         j .ge. fistart(n*3+1)+1 .and. j .le. fiend(n*3+1)+1) then
             idim = fiend(n*3+1) - fistart(n*3+1) + 1
c            do j=fistart(n*3+1)+1, fiend(n*3+1)+1
             offset = j-fistart(n*3+1) + (k-fjstart(n*3+1)-1)*idim
             array(dindex(n*6+1)+offset) = f1(lface(n*3+1)+1, j, k)*dt
             array(dindex(n*6+2)+offset) = f1(rface(n*3+1)+2, j, k)*dt
c            array(eindex(n*6+1)+offset) = ef(lface(n*3+1)+1, j)
c            array(eindex(n*6+2)+offset) = ef(rface(n*3+1)+2, j)
c            array(uindex(n*6+1)+offset) = uf(lface(n*3+1)+1, j)
c            array(uindex(n*6+2)+offset) = uf(rface(n*3+1)+2, j)
c            if (nyz .gt. 1) then
c              array(vindex(n*6+1)+offset) = vf(lface(n*3+1)+1, j)
c              array(vindex(n*6+2)+offset) = vf(rface(n*3+1)+2, j)
c            endif
c            if (nzz .gt. 1) then
c              array(windex(n*6+1)+offset) = wf(lface(n*3+1)+1, j)
c              array(windex(n*6+2)+offset) = wf(rface(n*3+1)+2, j)
c            endif
             do ic=1, ncolor
               array(colindex(n*6+1,ic)+offset) = 
     &                                 colstar(lface(n*3+1)+1, ic)*dt
               array(colindex(n*6+2,ic)+offset) = 
     &                                 colstar(rface(n*3+1)+2, ic)*dt
             enddo
           endif
         enddo
c
c        Next j line
c
         enddo
c
c       Next k slice
c
      enddo
c
c     Convert v,w momenta back to velocities
c
      do k = 1, kn
         do j = 1, jn
            jm1 = max(j-1, 1)
            km1 = max(k-1, 1)
            do i = is-1, ie+1
               v(i,j,k) = v(i,j,k)/(0.5*(d(i,j,k) + d(i,jm1,k)))
               w(i,j,k) = w(i,j,k)/(0.5*(d(i,j,k) + d(i,j,km1)))
               if (abs(v(i,j,k)) .gt. dx(i)/dt) then
                  write(6,*) 'vx',i,j,k,ie,je,ke,jm1
                  write(6,*) v(i,j,k),d(i,j,k),d(i,jm1,k)
               endif
               if (abs(w(i,j,k)) .gt. dx(i)/dt) then
                  write(6,*) 'wx',i,j,k,ie,je,ke,km1
                  write(6,*) w(i,j,k),d(i,j,k),d(i,j,km1)
               endif
            enddo
         enddo
      enddo
c
      return
      end
