!=======================================================================
!
! Copyright 2009 Daniel R. Reynolds
!
! This software is released under the terms of the "Enzo Public License"
! in the accompanying LICENSE file.
!
!=======================================================================
subroutine gFLDSplit_RadiationSource(Ersrc, time, a, ProbType, ESpectrum, &
     NGammaDot, EtaRadius, EtaCenter, aUnits, LenUnits, TimeUnits,        &
     ErUnits, Nx, Ny, Nz, NGxl, NGxr, NGyl, NGyr, NGzl, NGzr, x0L, x0R,   &
     x1L, x1R, x2L, x2R, ier)
!=======================================================================
!  written by: Daniel R. Reynolds
!  date:       July 2009
!
!  PURPOSE: Computes the emissivity point sources into the radiation 
!           energy equation
!
!  INPUTS:
!     time       - simulation time for evaluation
!     a          - cosmological expansion parameter
!     ProbType   - flag denoting physical problem to run
!     Nchem      - number of chemical species
!     HFrac      - percentage of mass composed of Hydrogen
!     ESpectrum  - radiation spectrum choice
!                       1 -> 1e5 black body spectrum
!                       0 -> power law spectrum
!                      -1 -> monochromatic at hnu0_HI
!                      -2 -> monochromatic at hnu0_HeI
!                      -3 -> monochromatic at hnu0_HeII
!     NGammaDot  - ionization source
!     EtaRadius  - ionization source radius in cells
!     EtaCenter  - ionization source center (comoving, 3D coordinates in cm)
!     *Units     - variable scaling constants
!     Nx,Ny,Nz   - active mesh size in each direction
!     NG*l/NG*r  - left/right ghost cells in each direction
!     x*L/x*R    - left/right subdomain boundaries (comoving, no ghosts)
!
!     Note: the vector inputs are of size (Nx + NGxl + NGxr) in 
!     the x-direction, others are similar.
!
!  OUTPUT ARGUMENTS: 
!     Ersrc      - array of emissivity sources
!     ier        - success/failure flag (1->success, 0->failure)
!
!  EXTERNALS: 
!
!  LOCALS:
!
!=======================================================================
  implicit none
#include "fortran.def"

  !--------------
  ! argument declarations
  integer, intent(in) :: ESpectrum, ProbType
  integer, intent(in) :: Nx, NGxl, NGxr
  integer, intent(in) :: Ny, NGyl, NGyr
  integer, intent(in) :: Nz, NGzl, NGzr
  integer, intent(out) :: ier
  REALSUB, intent(in) :: a
  REALSUBR, intent(in) :: time, NGammaDot, EtaRadius
  REALSUBR, intent(in) :: EtaCenter(3)
  REALSUBR, intent(in) :: aUnits, LenUnits, TimeUnits, ErUnits
  REALSUBR, intent(in) :: x0L, x0R, x1L, x1R, x2L, x2R
  REALSUBR, intent(out) :: Ersrc(1-NGxl:Nx+NGxr,1-NGyl:Ny+NGyr,1-NGzl:Nz+NGzr)
  
  !--------------
  ! locals
  integer :: i, j, k
  REALSUBR :: pi, h_nu0, h_nu1, h_nu2, etaconst, specconst
  REALSUBR :: dx, dy, dz, dV, cellXl, cellXr, cellYl, cellYr, cellZl, cellZr
  REALSUBR :: cellXc, cellYc, cellZc

  !=======================================================================

!!$  write(*,*) 'Entering gFLDSplit::RadiationSource routine'
!!$  write(*,*) '   NGammaDot = ',NGammaDot
!!$  write(*,*) '   EtaRadius = ',EtaRadius
!!$  write(*,*) '   EtaCenter = ',EtaCenter

  ! initialize output to have all zero values, flag to success
  Ersrc = 0.d0
  ier = 1
  
  ! initialize constants
  pi    = 4.D0*datan(1.D0)
  dx    = (x0R-x0L)/Nx                ! mesh spacing (comoving), x0 direction
  dy    = (x1R-x1L)/Ny                ! mesh spacing (comoving), x1 direction
  dz    = (x2R-x2L)/Nz                ! mesh spacing (comoving), x2 direction
  dV    = dx*dy*dz*(LenUnits)**3      ! cell volume (proper)
  h_nu0 = 13.6d0*1.60217653e-12       ! ionization energy of HI [ergs]
  h_nu1 = 24.6d0*1.60217653e-12       ! ionization energy of HeI [ergs]
  h_nu2 = 54.4d0*1.60217653e-12       ! ionization energy of HeII [ergs]

  ! scaling factor for relevant spectrum 
  !   = (int_{nu0}^{infty} chi(nu) dnu) / (int_{0}^{infty} chi(nu)*(nu0/nu) dnu)
  ! where chi(nu) is the SED for the emitting source 
  if (ESpectrum == 1) then
     ! T=10^5 blackbody 
     specconst = 1.52877652583602d0
  elseif (ESpectrum == 0) then
     ! power law: chi(nu)=0 for nu<nu0, chi(nu) = (nu/nu0)^(-1.5) otherwise
     specconst = 1.d0
  elseif (ESpectrum == -3) then
     ! monochromatic at nu=nu2
     specconst = h_nu2/h_nu0
  elseif (ESpectrum == -2) then
     ! monochromatic at nu=nu1
     specconst = h_nu1/h_nu0
  else
     ! monochromatic at nu=nu0
     specconst = 1.d0
  endif

  ! compute emissivity for various problems

  !   point-source emissivity at location (EtaCenter(1:3))
  if ((ProbType == 210) .or. (ProbType == 211) .or. &
      (ProbType == 213) .or. (ProbType == 215)) then

     ! one-cell source
     if (EtaRadius == 0.d0) then
        
        ! compute eta factor for given ionization source
        etaconst = h_nu0*NGammaDot*specconst/dV
        
        ! place ionization source in one cell
        do k=1,Nz,1
           
           ! z-boundaries (comoving) for this cell
           cellZl = x2L + (k-1)*dz
           cellZr = cellZl + dz
           
           do j=1,Ny,1
              
              ! y-boundaries (comoving) for this cell
              cellYl = x1L + (j-1)*dy
              cellYr = cellYl + dy
              
              do i=1,Nx,1
                 
                 ! x-boundaries (comoving) for this cell
                 cellXl = x0L + (i-1)*dx
                 cellXr = cellXl + dx
                 
                 ! see if domain center is in cell (or on left edge)
                 if ( (cellXl <= EtaCenter(1)) .and. (cellXr > EtaCenter(1)) .and. &
                      (cellYl <= EtaCenter(2)) .and. (cellYr > EtaCenter(2)) .and. &
                      (cellZl <= EtaCenter(3)) .and. (cellZr > EtaCenter(3)) ) then
                    Ersrc(i,j,k) = etaconst
                 endif
                 
              enddo
           enddo
        enddo

     else

        ! compute eta factor for given ionization source
        etaconst = h_nu0*NGammaDot*specconst/dV/8.d0/(EtaRadius**3)
        
        ! place ionization source in center of domain
        do k=1,Nz,1
           
           ! z-center (comoving) for this cell
           cellZc = x2L + (k-0.5d0)*dz
           
           do j=1,Ny,1
              
              ! y-center (comoving) for this cell
              cellYc = x1L + (j-0.5d0)*dy
              
              do i=1,Nx,1
                 
                 ! x-center (comoving) for this cell
                 cellXc = x0L + (i-0.5d0)*dx
                 
                 ! see if cell is within source region
                 if ( (abs(cellXc-EtaCenter(1)) < EtaRadius*dx) .and. &
                      (abs(cellYc-EtaCenter(2)) < EtaRadius*dy) .and. &
                      (abs(cellZc-EtaCenter(3)) < EtaRadius*dz) ) then
                    Ersrc(i,j,k) = etaconst
                 endif
                 
              enddo
           enddo
        enddo

     endif ! EtaRadius == 0

  !   emissivity flux along x=0 wall (NGammaDot photons/s/cm^2)
  else if (ProbType == 212) then

     ! place ionization source along left wall (if on this subdomain)
     if (x0L == 0.d0) then

        ! compute eta factor for given ionization source, and put on wall
        etaconst = h_nu0*NGammaDot*specconst/dy
        do k=1,Nz,1
           do j=1,Ny,1
              Ersrc(1,j,k) = etaconst
           enddo
        enddo
     endif
     
  !   point-source emissivity at center of every processor
  elseif (ProbType == 214) then

     ! compute eta factor for given ionization source
     etaconst = h_nu0*NGammaDot*specconst/dV
        
     ! place ionization source in center of subdomain
     Ersrc(int(Nx/2),int(Ny/2),int(Nz/2)) = etaconst
     
  !   homogeneous emissivity field w/ strength hnu0*NGammaDot/dV
  elseif (ProbType == 216) then

     Ersrc = h_nu0*NGammaDot*specconst/dV
     
  endif ! ProbType

!!$  write(*,*) 'RadiationSource: individual source is ',etaconst
!!$  etaconst = sum(Ersrc)
!!$  write(*,*) 'RadiationSource: integrated source is ',etaconst
  
  return
end subroutine gFLDSplit_RadiationSource
!=======================================================================
